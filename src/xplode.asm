	ORG	6E40H
SHPPOS	EQU	60F5H
RNDNO	EQU	6109H
CONTER	EQU	6107H
SET	EQU	66F3H
STARTX	EQU	676BH
STARTY	EQU	676DH
REVERS	EQU	5B2DH
STORS1	EQU	6777H
ED00H	EQU	6249H
SOUND	EQU	5E2AH
SOUND1	EQU	5E2FH
SOUND2	EQU	5E39H
SHPPS1	EQU	60FAH
SHEET	EQU	555AH
P1OR2F	EQU	60F1H
NOPLAY	EQU	6100H
DEMOF	EQU	60F2H
NUSHIP	EQU	6106H
SCREEN	EQU	5000H
PLY1NO	EQU	SCREEN+11
PLY2NO	EQU	SCREEN+3EH
SCORE1	EQU	SCREEN+6
SCORE2	EQU	SCREEN+39H
TMLNPT	EQU	6281H
TIMLIN	EQU	6113H
SPEED	EQU	60FEH
SPEEDX	EQU	565AH
GYTYP	EQU	60EFH
PRNSTR	EQU	619CH
SHOOTF	EQU	610CH
FINTO1	EQU	6052H
SHTSPD	EQU	594EH	;BULLET+4
CZPFRE	EQU	643CH
TIMSPD	EQU	568FH	;LDHLTC+6
BPOINT	EQU	60E3H
CHANCE	EQU	6494H
CHNCE1	EQU	6582H
NOOFSH	EQU	64B3H
NOFSH1	EQU	6599H
TITLEP	EQU	6780H
NUMHI1	EQU	6DC0H
NUMHI5	EQU	6DD8H
NUMHI4	EQU	6DD2H
HINM5	EQU	6E0EH
HINM4	EQU	6E02H
HINM1	EQU	6DDEH
VCTRHI	EQU	7880H
SCPTTB	EQU	6139H
DEAD	LD	HL,SCPTTB
	LD	DE,SCPTTB+1
	LD	BC,38
	LD	(HL),0
	LDIR
	LD	A,(P1OR2F)
	LD	HL,PLY1NO-1
	OR	A
	JR	Z,DEAD1
	LD	HL,PLY2NO-1
DEAD1	INC	HL
	DEC	(HL)
	LD	A,(HL)
	CP	'0'
	DEC	HL
	JR	NC,DEAD2
	DEC	(HL)
	INC	HL
	LD	A,(HL)
	ADD	A,10
	LD	(HL),A
	DEC	HL
DEAD2	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	LD	HL,3030H
	EX	DE,HL
	AND	A
	SBC	HL,DE
	LD	A,L
	SLA	H
	SLA	H
	SLA	H
	SLA	H
	ADD	A,H
	LD	(PLYNO),A
BEGIN	LD	HL,SCREEN
	LD	DE,3C00H
	LD	BC,40H
	LDIR
START	CALL	REVERS
	CALL	RNDOM
	SLA	C
	LD	B,0
	INC	C
	LD	(SOUND1),BC
	LD	(SOUND2),BC
	CALL	SOUND
	CALL	SOUND
	CALL	REVERS
	CALL	RNDOM
	SLA	C
	LD	B,0
	INC	C
	LD	(SOUND1),BC
	LD	(SOUND2),BC
	CALL	SOUND
	CALL	SOUND
	LD	A,(SHPPS1)
	ADD	A,6
	LD	IX,PNTTBL
	LD	B,2
STPNT1	PUSH	BC
	LD	B,30
SETPNT	PUSH	BC
	LD	(IX+0),1
	LD	(IX+3),47
	LD	(IX+4),A
	CALL	RNDOM
	LD	(IX+2),C
	CALL	RNDOM
	SET	7,C
	LD	(IX+1),C
	LD	B,5
	INC	IX
	DJNZ	$-2
	POP	BC
	DJNZ	SETPNT
	INC	A
	POP	BC
	DJNZ	STPNT1
CTPNT	LD	IX,PNTTBL
	LD	B,60
	LD	A,0
	LD	(CTPTOT),A
CNTPNT	PUSH	BC
	CALL	RNDOM
	SLA	C
	INC	C
	LD	B,0
	LD	(SOUND1),BC
	LD	(SOUND2),BC
	CALL	SOUND
	CALL	SOUND
	LD	A,(IX+0)
	OR	A
	JR	NZ,PNTCNT
	LD	A,(CTPTOT)
	INC	A
	LD	(CTPTOT),A
	JR	CNTPT1
PNTCNT	LD	A,1
	LD	(STORS1),A
	LD	A,(IX+3)
	LD	(STARTY+1),A
	LD	A,(IX+4)
	LD	(STARTX+1),A
	CALL	SET
	CALL	RNDMTY
	CALL	ADDX
	CALL	ADDY
	LD	A,(IX+0)
	OR	A
	JR	NZ,PTCNT1
	LD	A,(CTPTOT)
	INC	A
	LD	(CTPTOT),A
	JR	CNTPT1
PTCNT1	LD	A,(IX+3)
	LD	(STARTY+1),A
	LD	A,(IX+4)
	LD	(STARTX+1),A
	LD	A,0
	LD	(STORS1),A
	CALL	SET
CNTPT1	LD	B,5
	INC	IX
	DJNZ	$-2
	POP	BC
	DJNZ	CNTPNT
	LD	A,(CTPTOT)
	CP	60
	JP	Z,DEAD86
	JP	CTPNT
RNDOM	PUSH	AF
	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	LD	(RNDNO),A
	LD	A,H
	CP	2FH
	JR	NZ,NORST
	LD	H,0
NORST	LD	(CONTER),HL
	LD	A,(RNDNO)
	AND	87H
	LD	C,A
	POP	AF
	RET
ADDX	LD	C,(IX+4)
	LD	A,(IX+2)
	SRL	A
	SRL	A
	SRL	A
	AND	1
	SLA	C
	ADD	A,C
	LD	B,A
	LD	A,(IX+2)
	AND	7
	LD	C,A
	LD	A,B
	BIT	7,(IX+2)
	JR	Z,ADDPOS
	SUB	C
	JR	ADDPS1
ADDPOS	ADD	A,C
ADDPS1	JR	C,CKXOT1
	SRL	A
	LD	(IX+4),A
	JR	C,SET4X2
	RES	3,(IX+2)
	JR	CKXOUT
SET4X2	SET	3,(IX+2)
	RET
CKXOT1	LD	(IX+0),0
CKXOUT	RET
ADDY	LD	C,(IX+3)
	LD	A,(IX+1)
	SRL	A
	SRL	A
	SRL	A
	AND	1
	SLA	C
	ADD	A,C
	LD	C,A
	LD	A,(IX+1)
	AND	7
	BIT	7,(IX+1)
	JR	Z,ADDPLS
	NEG
ADDPLS	ADD	A,C
	SRL	A
	LD	(IX+3),A
	JR	C,SET4X1
	RES	3,(IX+1)
	JR	CKYOUT
SET4X1	SET	3,(IX+1)
CKYOUT	CP	3
	JR	C,CKYOT1
	AND	30H
	CP	30H
	RET	NZ
CKYOT1	LD	(IX+0),0
	RET
RNDMTY	CALL	RNDOM
	BIT	7,C
	RET	NZ
	LD	A,(IX+1)
	AND	7
	JR	NZ,RDMTY1
	RES	7,(IX+1)
RDMTY1	BIT	7,(IX+1)
	JR	Z,INCMTY
	DEC	(IX+1)
	RET
INCMTY	INC	(IX+1)
	RET
DEAD86	LD	A,(PLYNO)
	OR	A
	JR	NZ,EXCHNG
	CALL	GMOVER
	LD	A,(NOPLAY)
	OR	A
	JP	Z,HISCCK
	LD	A,(PLYNO)
	LD	B,A
	LD	A,(PLYNO1)
	OR	B
	JP	Z,HISCCK
EXCHNG	LD	A,(NOPLAY)
	OR	A
	JR	Z,EXCHN1
	LD	A,(GYTYP)
	EX	AF,AF'
	LD	A,(GYTYP1)
	LD	(GYTYP),A
	EX	AF,AF'
	LD	(GYTYP1),A
	LD	A,(NUSHIP)
	EX	AF,AF'
	LD	A,(NUSHP1)
	LD	(NUSHIP),A
	EX	AF,AF'
	LD	(NUSHP1),A
	LD	A,(PLYNO)
	EX	AF,AF'
	LD	A,(PLYNO1)
	LD	(PLYNO),A
	EX	AF,AF'
	LD	(PLYNO1),A
	LD	A,(P1OR2F)
	XOR	1
	LD	(P1OR2F),A
EXCHN1	LD	A,3AH
	LD	(SHPPOS),A
	LD	(SHPPS1),A
	LD	A,(PLYNO)
	OR	A
	JR	Z,EXCHNG
	LD	A,46
	LD	(TIMLIN),A
	CALL	TMLNPT
	LD	BC,3000H
	CALL	60H
	CALL	SPFX
	CALL	PLYMS
	JP	SHEET
GMOVER	LD	HL,3D54H
	LD	DE,3D55H
	LD	BC,15H
	LD	(HL),80H
	LDIR
	LD	HL,GMOVMS
	LD	DE,7956H-SCREEN
	CALL	PRNSTR
	LD	A,(P1OR2F)
	ADD	A,31H
	LD	(3D67H),A
	LD	BC,0B000H
	CALL	60H
	LD	HL,3D55H
	LD	DE,3D56H
	LD	BC,19
	LD	(HL),80H
	LDIR
	LD	BC,4800H
	JP	60H
PLYMS	LD	HL,3D58H
	LD	DE,3D59H
	LD	BC,0AH
	LD	(HL),80H
	LDIR
	LD	HL,PLYRMS
	LD	DE,795AH-SCREEN
	CALL	PRNSTR
	LD	A,(P1OR2F)
	ADD	A,31H
	LD	(3D61H),A
	LD	BC,6000H
	CALL	60H
	LD	HL,3D59H
	LD	DE,3D5AH
	LD	BC,10
	LD	(HL),80H
	LDIR
	LD	BC,2800H
	JP	60H
SPFX	LD	HL,SCREEN+40H
	LD	DE,SCREEN+41H
	LD	BC,03CFH
	LD	(HL),80H
	LDIR
	LD	A,(SPFXCN)
	INC	A
	AND	3
	LD	(SPFXCN),A
	OR	A
	JR	Z,SIDEFX
	DEC	A
	JR	Z,UPFX
	DEC	A
	JR	Z,NOFX
	DEC	A
	JP	Z,FNGRFX
SIDEFX	LD	A,0BFH
SIDFX3	LD	B,65
SIDFX2	PUSH	BC
	LD	B,16
	LD	HL,3C00H
	LD	DE,3BFFH
SIDFX1	PUSH	BC
	INC	HL
	INC	DE
	LD	BC,63
	LDIR
	LD	(DE),A
	POP	BC
	DJNZ	SIDFX1
	PUSH	HL
	PUSH	AF
	LD	HL,30H
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	CALL	SOUND
	CALL	SOUND
	CALL	SOUND
	POP	AF
	POP	HL
	POP	BC
	DJNZ	SIDFX2
	CP	80H
	JR	Z,RET
	LD	A,80H
	JR	SIDFX3
NOFX	LD	HL,3C00H
	LD	DE,3C01H
	LD	BC,03FFH
	LD	(HL),80H
	LDIR
	LD	HL,30H
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	BC,2000H
	CALL	60H
	LD	B,9
NOFXSN	PUSH	BC
	CALL	SOUND
	POP	BC
	DJNZ	NOFXSN
	JR	RET
UPFX	LD	A,0BFH
UPFX8	LD	B,17
UPFX1	PUSH	BC
	LD	HL,3FBFH
	LD	DE,3FFFH
	LD	BC,3C0H
	LDDR
	LD	HL,3C00H
	LD	DE,3C01H
	LD	BC,3FH
	LD	(HL),A
	LDIR
	LD	L,H
	LD	H,0
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,10H
UPFX2	PUSH	BC
	PUSH	AF
	CALL	SOUND
	POP	AF
	POP	BC
	DJNZ	UPFX2
	POP	BC
	DJNZ	UPFX1
	CP	80H
	JR	Z,RET
	LD	A,80H
	JR	UPFX8
RET	LD	HL,SCREEN
	LD	DE,3C00H
	LD	BC,40H
	LDIR
	LD	A,1
	LD	(SHOOTF),A
	CALL	FINTO1
	RET
FNGRFX	LD	A,80H
	LD	HL,3C01H
	LD	DE,3C00H
	LD	B,65
FNGFX2	PUSH	BC
	LD	B,8
FNGFX1	PUSH	BC
	LD	BC,3FH
	LDIR
	LD	(DE),A
	EX	DE,HL
	LD	BC,3FH
	ADD	HL,BC
	LD	E,L
	LD	D,H
	INC	DE
	LDDR
	LD	(DE),A
	EX	DE,HL
	LD	BC,41H
	ADD	HL,BC
	LD	D,H
	LD	E,L
	DEC	DE
	POP	BC
	DJNZ	FNGFX1
	LD	L,H
	LD	H,0
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,5
FNGSND	PUSH	BC
	PUSH	AF
	CALL	SOUND
	POP	AF
	POP	BC
	DJNZ	FNGSND
	LD	HL,3C01H
	LD	DE,3C00H
	POP	BC
	DJNZ	FNGFX2
	JR	RET
GMOVMS	DEFM	'Game Over '
PLYRMS	DEFM	'Player '
	DEFB	0
LEVELS	LD	A,(P1OR2F)
	OR	A
	LD	HL,SCORE1-5
	JR	Z,LEVLS1
	LD	HL,SCORE2-5
LEVLS1	LD	A,(HL)
	INC	HL
	SUB	'0'
	JR	Z,LEV0X0
	DEC	A
	JR	Z,LEV1X0
	DEC	A
	JR	Z,LEV2X0
LEV3X0	LD	A,20
LEV3L0	LD	(LEVEL),A
	JR	LEVFIX
LEV2X0	LD	B,18
	CALL	LEVCK1
	LD	A,B
	JR	LEV3L0
LEV0X0	LD	A,(HL)
	INC	HL
	SUB	'0'
	CP	5
	JR	NC,LEV0H0
	SLA	A
	LD	B,A
	CALL	LEVCK1
	LD	A,B
	JR	LEV3L0
LEV0H0	ADD	A,5
	JR	LEV3L0
LEV1X0	LD	B,15
	CALL	LEVCHK
	LD	A,B
	JR	LEV3L0
LEVCHK	LD	A,(HL)
	SUB	'0'
	CP	2
	RET	C
	INC	B
LEVCK1	LD	A,(HL)
	SUB	'0'
	CP	5
	RET	C
	INC	B
	RET
LEVFIX	LD	A,(LEVEL)
	CP	8
	PUSH	AF
	JR	C,LEVFX1
	LD	A,38H
	JR	LEVFX2
LEVFX1	LD	A,20H
LEVFX2	LD	(SHTSPD),A
	POP	AF
	LD	E,A
	LD	D,0
	LD	HL,CZPTBL
	ADD	HL,DE
	LD	A,(HL)
	LD	(CZPFRE),A
	LD	HL,TIMTBL
	ADD	HL,DE
	LD	A,(HL)
	LD	(TIMSPD),A
	LD	HL,SPDTBL
	ADD	HL,DE
	LD	A,(HL)
	LD	(SPEED+1),A
	LD	H,D
	LD	L,E
	ADD	HL,HL
	ADD	HL,DE
	EX	DE,HL
	LD	HL,BTBL
	ADD	HL,DE
	LD	DE,BPOINT	
	LD	BC,3
	LDIR
	LD	HL,(LEVEL)
	LD	H,0
	ADD	HL,HL
	EX	DE,HL
	LD	A,(GYTYP)
	LD	HL,EYETBL
	DEC	A
	JR	Z,PUTIT
	LD	HL,FITTBL
	DEC	A
	JR	Z,PUTIT
	LD	HL,DRNTBL
	DEC	A
	JR	Z,PUTIT
	LD	HL,XCUTBL
	DEC	A
	RET	Z
	DEC	A
	JR	Z,PUTIT
	RET
PUTIT	ADD	HL,DE
	LD	A,(HL)
	LD	(CHANCE),A
	LD	(CHNCE1),A
	INC	HL
	LD	A,(HL)
	LD	(NOOFSH),A
	LD	(NOFSH1),A
	RET
CTPTOT	DEFB	0
PLYNO	DEFB	3
PLYNO1	DEFB	3
GYTYP1	DEFB	1
NUSHP1	DEFB	0
SPFXCN	DEFB	0
LEVEL	DEFB	0
EYETBL	DEFW	21FH
	DEFW	31FH
	DEFW	20FH
	DEFW	30FH
	DEFW	40FH
	DEFW	50FH
	DEFW	307H
	DEFW	407H
	DEFW	507H
	DEFW	607H
	DEFW	707H
	DEFW	403H
	DEFW	503H
	DEFW	603H
	DEFW	703H
	DEFW	501H
	DEFW	601H
	DEFW	701H
	DEFW	801H
	DEFW	800H
	DEFW	800H
FITTBL	DEFW	31FH
	DEFW	20FH
	DEFW	30FH
	DEFW	40FH
	DEFW	50FH
	DEFW	307H
	DEFW	407H
	DEFW	507H
	DEFW	607H
	DEFW	707H
	DEFW	403H
	DEFW	503H
	DEFW	603H
	DEFW	703H
	DEFW	501H
	DEFW	601H
	DEFW	701H
	DEFW	801H
	DEFW	800H
	DEFW	800H
	DEFW	800H
DRNTBL	DEFW	11FH
	DEFW	21FH
	DEFW	31FH
	DEFW	20FH
	DEFW	30FH
	DEFW	40FH
	DEFW	50FH
	DEFW	307H
	DEFW	407H
	DEFW	507H
	DEFW	607H
	DEFW	707H
	DEFW	403H
	DEFW	503H
	DEFW	603H
	DEFW	703H
	DEFW	501H
	DEFW	601H
	DEFW	701H
	DEFW	801H
	DEFW	800H
XCUTBL	DEFW	11FH
	DEFW	21FH
	DEFW	31FH
	DEFW	20FH
	DEFW	30FH
	DEFW	207H
	DEFW	307H
	DEFW	407H
	DEFW	203H
	DEFW	303H
	DEFW	403H
	DEFW	301H
	DEFW	401H
	DEFW	300H
	DEFW	400H
	DEFW	400H
	DEFW	400H
	DEFW	400H
	DEFW	400H
	DEFW	400H
	DEFW	400H
CZPTBL	DEFW	1F3FH
	DEFW	0F1FH
	DEFW	0F0FH
	DEFW	070FH
	DEFW	0707H
	DEFW	0707H
	DEFW	0303H
	DEFW	303H
	DEFW	101H
	DEFW	101H
	DEFB	0
TIMTBL	DEFB	18H
	DEFB	18H
	DEFB	18H
	DEFB	14H
	DEFB	14H
	DEFB	14H
	DEFB	14H
	DEFB	10H
	DEFB	10H
	DEFB	10H
	DEFB	10H
	DEFB	10H
	DEFB	0CH
	DEFB	0CH
	DEFB	0CH
	DEFB	0CH
	DEFB	0CH
	DEFB	08H
	DEFB	08H
	DEFB	08H
	DEFB	04H
SPDTBL	DEFW	404H
	DEFB	4
	DEFW	303H
	DEFB	3
	DEFW	202H
	DEFB	2
	DEFW	102H
	DEFW	101H
	DEFB	1
	DEFW	101H
	DEFW	101H
	DEFW	101H
	DEFB	1
BTBL	DEFW	100H
	DEFB	0
	DEFW	100H
	DEFB	0
	DEFW	200H
	DEFB	0
	DEFW	200H
	DEFB	0
	DEFW	200H
	DEFB	0
	DEFW	400H
	DEFB	0
	DEFW	400H
	DEFB	0
	DEFW	400H
	DEFB	0
	DEFW	400H
	DEFB	0
	DEFW	0
	DEFB	1
	DEFW	0
	DEFB	1
	DEFW	0
	DEFB	1
	DEFW	500H
	DEFB	1
	DEFW	500H
	DEFB	1
	DEFW	500H
	DEFB	1
	DEFW	0
	DEFB	2
	DEFW	0
	DEFB	2
	DEFW	0
	DEFB	2
	DEFW	0
	DEFB	5
	DEFW	0
	DEFB	5
	DEFW	0
	DEFB	5
HISCCK	LD	A,0C9H
	LD	(RET),A
	CALL	SPFX
	LD	A,21H
	LD	(RET),A
	LD	HL,VCTRHI
	LD	DE,4000H
	LD	BC,40H
	LDIR
	LD	A,(DEMOF)
	OR	A
	JP	NZ,TITLEP
HISCK1	LD	HL,SCORE2-5
	LD	DE,SCORE1-5
	CALL	COMPAR
	LD	A,5
	LD	(PERMUT),A
	LD	A,(PLAYER)
	ADD	A,'0'
	LD	(MESSAG+27),A
	LD	C,L
	LD	B,H
	LD	DE,NUMHI1
HISCK2	PUSH	BC
	CALL	COMPAR
	POP	BC
	PUSH	HL
	AND	A
	SBC	HL,BC
	POP	HL
	JR	Z,MOVBAK
	LD	A,(PERMUT)
	DEC	A
	LD	(PERMUT),A
	JP	Z,TITLEP
	PUSH	BC
	LD	BC,6
	ADD	HL,BC
	POP	BC
	EX	DE,HL
	LD	L,C
	LD	H,B
	JR	HISCK2
MOVBAK	LD	A,(PERMUT)
	DEC	A
	LD	(PERMUT),A
	LD	(SCRPOS),HL
	LD	HL,MESSAG
	LD	DE,78D0H-SCREEN
	CALL	PRNSTR
	INC	HL
	LD	DE,7949H-SCREEN
	CALL	PRNSTR
	INC	HL
	LD	DE,79CFH-SCREEN
	CALL	PRNSTR
	INC	HL
	LD	DE,7A5BH-SCREEN
	CALL	PRNSTR
	LD	B,8
	LD	HL,BUFFER
	PUSH	BC
	PUSH	HL
	LD	D,H
	LD	E,L
	INC	DE
	LD	(HL),20H
	LD	BC,8
	LDIR
	POP	HL
	POP	BC
	LD	DE,3E5DH
	LD	(4020H),DE
	CALL	40H
	CALL	CHKBUF
	LD	DE,NUMHI5+5
	LD	HL,NUMHI4+5
	LD	A,(PERMUT)
	LD	C,A
	LD	B,0
	PUSH	HL
	LD	L,C
	LD	H,B
	ADD	HL,HL
	ADD	HL,BC
	ADD	HL,HL
	LD	C,L
	LD	B,H
	POP	HL
	LD	A,B
	OR	C
	JR	Z,MVBK1
	LDDR
MVBK1	INC	HL
	EX	DE,HL
	LD	HL,(SCRPOS)
	LD	BC,6
	LDIR
	LD	HL,(SCRPOS)
	LD	DE,(SCRPOS)
	INC	DE
	LD	(HL),'0'
	LD	BC,5
	LDIR
	LD	A,(PERMUT)
	LD	C,A
	LD	B,0
	LD	L,C
	LD	H,B
	ADD	HL,HL
	ADD	HL,BC
	ADD	HL,HL
	ADD	HL,HL
	LD	C,L
	LD	B,H
	LD	DE,HINM5+11
	LD	HL,HINM4+11
	LD	A,B
	OR	C
	JR	Z,MVBAK6
	LDDR
MVBAK6	INC	HL
	INC	HL
	INC	HL
	INC	HL
	EX	DE,HL
	LD	HL,BUFFER
	LD	BC,8
	LDIR
	LD	B,5
	LD	A,'0'
	LD	HL,HINM1
LOOPY1	INC	A
	LD	(HL),A
	LD	DE,12
	ADD	HL,DE
	DJNZ	LOOPY1
	LD	A,0C9H
	LD	(RET),A
	CALL	SPFX
	LD	A,21H
	LD	(RET),A
	JP	HISCK1
COMPAR	PUSH	HL
	PUSH	DE
	LD	B,6
COMPR2	LD	A,(DE)
	CP	(HL)
	JR	C,HLBIGR
	JR	NZ,DEBIGR
	INC	HL
	INC	DE
	DJNZ	COMPR2
	POP	HL
	POP	DE
	LD	A,1
	LD	(PLAYER),A
	RET
DEBIGR	POP	HL
	POP	DE
	LD	A,1
	LD	(PLAYER),A
	RET
HLBIGR	POP	DE
	POP	HL
	LD	A,2
	LD	(PLAYER),A
	RET
CHKBUF	PUSH	HL
	PUSH	BC
	LD	HL,BUFFER
	LD	B,8
CHKBF2	LD	A,(HL)
	CP	20H
	JR	NC,DJNZBF
CHKBF1	LD	(HL),20H
	INC	HL
	DJNZ	CHKBF1
	POP	BC
	POP	HL
	RET
DJNZBF	INC	HL
	DJNZ	CHKBF2
	POP	BC
	POP	HL
	RET
MESSAG	DEFM	'>>> Congratulations Player   <<<'
	DEFB	0
	DEFM	'You have one of the top five scores of today.'
	DEFB	0
	DEFM	'Please enter your name or initials.'
	DEFB	0
	DEFM	'? '
	DEFB	0
BUFFER	DEFS	10
PERMUT	DEFB	0
SCRPOS	DEFW	0
PLAYER	DEFB	0
PNTTBL	DEFS	310
LAST	EQU	$
	END	DEAD

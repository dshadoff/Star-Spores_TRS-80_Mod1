	ORG	6780H
DISTRB	EQU	4F00H
LOWCAS	EQU	6215H
DEMOF	EQU	60F2H
NOPLAY	EQU	6100H
MOVE	EQU	54A0H

FIXSTR	LD	HL,3C00H
	LD	(HL),0FFH
	LD	A,(HL)
	CP	0FFH
	JR	NZ,NOLOW
	LD	A,1
	LD	(LOWCAS),A
	JR	START
NOLOW	XOR	A
	LD	(LOWCAS),A
START	LD	A,9
	LD	(NUMLO),A
	ADD	A,50
	LD	(NUMHI),A
	LD	HL,80H
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	XOR	A
	LD	(BANCNT),A
	LD	HL,DISTRB
	LD	(MSBNLD),HL
	LD	HL,POPINS
	LD	(PINJP1),HL
	LD	(PINJP2),HL
	LD	HL,3C00H
	LD	DE,3C01H
	LD	BC,03FFH
	LD	(HL),80H
	LDIR
	LD	HL,3C00H
	LD	DE,3C01H
	LD	BC,3FH
	LD	(HL),83H
	LDIR
	LD	HL,3F80H
	LD	DE,3F81H
	LD	BC,3FH
	LD	(HL),8CH
	LDIR
	LD	DE,3E98H
	LD	HL,BYDAVE
	CALL	PRNSTR
	LD	IX,3C00H
	LD	DE,64
	LD	B,14
VERTCL	LD	(IX),0BFH
	LD	(IX+63),0BFH
	ADD	IX,DE
	DJNZ	VERTCL
	LD	(IX),8FH
	LD	(IX+63),8FH
	LD	B,5
	LD	HL,NUMHI1
	LD	HL,3EC1H
	LD	DE,3EC2H
	LD	BC,3DH
	LD	(HL),83H
	LDIR
	LD	IX,3ED5H
	LD	B,3
	LD	DE,64
HITBL1	LD	(IX),0BFH
	LD	(IX+21),0BFH
	ADD	IX,DE
	DJNZ	HITBL1
	LD	(IX),8FH
	LD	(IX+21),8FH
	LD	HL,HISMSG
	LD	DE,3F03H
	CALL	PRNSTR
NMPUT	LD	HL,HINM1
	LD	DE,3F17H
	CALL	PRNSTR
	INC	HL
	LD	DE,3F2CH
	CALL	PRNSTR
	INC	HL
	LD	DE,3F42H
	CALL	PRNSTR
	INC	HL
	LD	DE,3F57H
	CALL	PRNSTR
	INC	HL
	LD	DE,3F6CH
	CALL	PRNSTR
	LD	HL,NUMHI1
	LD	DE,3F23H
	LD	BC,6
	LDIR
	LD	DE,3F38H
	LD	BC,6
	LDIR
	LD	DE,3F4EH
	LD	BC,6
	LDIR
	LD	DE,3F63H
	LD	BC,6
	LDIR
	LD	DE,3F78H
	LD	BC,6
	LDIR
WRITE	LD	HL,TABLE
GETNXT	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	A,D
	OR	E
	JP	Z,FINIT
	LD	B,3
DONEXT	PUSH	BC
	XOR	A
	EX	DE,HL
	PUSH	HL
	PUSH	DE
	PUSH	AF
	CALL	SETRST
	POP	AF
	POP	DE
	DEFB	6
NUMBER	DEFB	0
	LD	A,(NUMBER)
	INC	A
	INC	A
	INC	A
	CP	59
NUMHI	EQU	$-1
	JR	NZ,NUMPUT
	LD	A,9
NUMLO	EQU	$-1
NUMPUT	LD	(NUMBER),A
	LD	A,(NUMLO)
	ADD	A,2
	LD	(NUMLO),A
	ADD	A,50
	LD	(NUMHI),A
	LD	A,2
	OUT	(0FFH),A
	LD	A,1
	DJNZ	$
	OUT	(0FFH),A
	INC	HL
	POP	HL
	EX	DE,HL
	INC	D
	POP	BC
	LD	A,(3840H)
	AND	80H
	JP	NZ,BANNER
	LD	A,(3810H)
	AND	6
	JP	NZ,BANNER
	DJNZ	DONEXT
	JR	GETNXT
FINIT	JP	BANNER
;--------------------------------------------------------
POPINS	POP	HL
	POP	AF
	LD	HL,DISTRB+33
	LD	(MSBNLD),HL
	LD	HL,POPDEM
	LD	(PINJP1),HL
	LD	HL,PINJP2+2
	LD	(PINJP2),HL
BEGIN	LD	B,7FH
HERE	LD	HL,(SOUND1)
	DEC	HL
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	PUSH	BC
	CALL	SOUND
	CALL	SOUND
	POP	BC
	DJNZ	HERE
	JR	CONTIN
SOUND	LD	A,1
	OUT	(0FFH),A
	LD	BC,80H
SOUND1	EQU	$-2
	CALL	60H
	LD	A,2
	OUT	(0FFH),A
	LD	BC,80H
SOUND2	EQU	$-2
	CALL	60H
	RET
CONTIN	LD	HL,3FC0H
	LD	DE,3FC1H
	LD	BC,3FH
	LD	(HL),80H
	LDIR
	LD	HL,3C01H
	LD	DE,3C02H
	LD	BC,3DH
	LD	(HL),83H
	LDIR
	LD	HL,MSGIN0
	LD	DE,3C49H
	CALL	PRNSTR
	LD	HL,MSGIN1
	LD	DE,3C8BH
	CALL	PRNSTR
	LD	HL,MSGIN2
	LD	DE,3CC5H
	CALL	PRNSTR
	LD	HL,MSGIN3
	LD	DE,3D02H
	CALL	PRNSTR
	LD	HL,MSGIN4
	LD	DE,3D42H
	CALL	PRNSTR
	LD	HL,MSGIN5
	LD	DE,3D82H
	CALL	PRNSTR
	LD	HL,MSGIN6
	LD	DE,3DC5H
	CALL	PRNSTR
	LD	HL,MSGIN7
	LD	DE,3E01H
	CALL	PRNSTR
	LD	HL,3E41H
	LD	DE,3E42H
	LD	BC,3DH
	LD	(HL),80H
	LDIR
	LD	HL,MSGIN8
	LD	DE,3E41H
	CALL	PRNSTR
	LD	HL,MSGINA
	LD	DE,3E81H
	CALL	PRNSTR
	XOR	A
	LD	(BANCNT),A
	JP	BANNER
;--------------------------------------------------------
PRNSTR	PUSH	AF
PRNST1	LD	A,(HL)
	OR	A
	JR	NZ,STAY
	POP	AF
	RET
STAY	CP	20H
	JR	C,CRSRMV
	CP	0C0H
	JR	NZ,ONWARD
	LD	A,80H
	JR	ALPHA
ONWARD	CP	80H
	JR	NC,GRPHIX
	CP	60H
	JR	C,ALPHA
	PUSH	AF
	LD	A,(LOWCAS)
	OR	A
	JR	NZ,ALPHA1
	POP	AF
	SUB	20H
	JR	ALPHA
ALPHA1	POP	AF
ALPHA	LD	(DE),A
	INC	HL
	INC	DE
	JR	PRNST1
CRSRMV	CP	26
	JR	NZ,BACKSP
	PUSH	HL
	LD	HL,64
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	INC	HL
	JR	CHEKCR
BACKSP	CP	24
	JR	NZ,ROMCAL
	DEC	DE
	INC	HL
	JR	CHEKCR
ROMCAL	LD	(4020H),DE
	CALL	33H
	LD	DE,(4020H)
	INC	HL
	JR	CHEKCR
GRPHIX	EX	DE,HL
	OR	(HL)
	EX	DE,HL
	LD	(DE),A
	INC	DE
	INC	HL
	JR	PRNST1
CHEKCR	LD	A,D
	CP	40H
	JR	NC,SUBTRT
	CP	3CH
	JR	C,ADDITN
	JR	PRNST1
SUBTRT	PUSH	HL
	LD	HL,64
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	POP	HL
	JR	PRNST1
ADDITN	INC	DE
	JR	PRNST1
;--------------------------------------------------------
BANNER	PUSH	AF
	PUSH	HL
	LD	A,(BANCNT)
	CP	6
	JP	Z,POPINS
PINJP1	EQU	$-2
	INC	A
	LD	(BANCNT),A
	POP	HL
	POP	AF
	LD	HL,DISTRB
MSBNLD	EQU	$-2
BANER1	PUSH	HL
	LD	HL,3FC1H
	LD	DE,3FC0H
	LD	BC,63
	LDIR
	POP	HL
	LD	A,(HL)
	PUSH	AF
	LD	A,(LOWCAS)
	OR	A
	JR	NZ,BANPUT
	POP	AF
	CP	60H
	JR	C,BANPT1
	SUB	20H
	JR	BANPT1
BANPUT	POP	AF
BANPT1	LD	(DE),A
	INC	HL
	LD	A,(HL)
	OR	A
	PUSH	AF
	PUSH	HL
	LD	A,(3840H)
	AND	80H
	JP	NZ,POPINS
PINJP2	EQU	$-2
	LD	A,(3810H)
	AND	6
	JR	Z,POPBAN
	CP	2
	JP	Z,POPPL1
	JP	POPPL2
POPBAN	LD	BC,1800H
	CALL	60H
	POP	HL
	POP	AF
	JR	Z,BANNER
	JR	BANER1
	DEFB	0
BANCNT	DEFB	0
MSGBAN	DEFM	'Press <SPACE> for instructions.  To play, press "1" or "2" for the number of players.  Copyright (C) by Dave Shadoff, 1982.  All rights reserved.         '
	DEFB	0
LAST	EQU	$
;--------------------------------------------------------
	ORG	LAST
SETRST	PUSH	AF
	LD	E,H
	LD	A,L
	SRL	E
	LD	D,0
	JR	NC,SET10
	INC	D
SET10	LD	B,0FFH
SET20	INC	B
	SUB	3
	JP	P,SET20
SET25	ADD	A,3
	RLCA
	ADD	A,D
SET27	LD	C,A
	LD	L,B
	LD	H,0
	LD	B,6
SET30	ADD	HL,HL
	DJNZ	SET30
SET32	LD	D,0
	ADD	HL,DE
	LD	DE,3C00H
SET34	ADD	HL,DE
	LD	B,0
	POP	AF
	OR	A
	JR	NZ,RESET
	LD	IX,MASK
	ADD	IX,BC
	LD	A,(HL)
	OR	(IX)
SET36	LD	(HL),A
	RET
RESET	LD	IX,MASK1
	ADD	IX,BC
	LD	A,(HL)
	AND	(IX)
	JR	SET36
MASK	DEFB	81H
	DEFB	82H
	DEFB	84H
	DEFB	88H
	DEFB	90H
	DEFB	0A0H
MASK1	DEFB	0FEH
	DEFB	0FDH
	DEFB	0FBH
	DEFB	0F7H
	DEFB	0EFH
	DEFB	0DFH
BYDAVE	DEFM	'By Dave Shadoff'
	DEFB	0H
HISMSG	DEFM	'Top Five Scores:'
	DEFB	0H
TABLE	EQU	7CFCH
MSGIN0	DEFM	'>>>>>   STAR SPORES - by Dave Shadoff   <<<<<'
	DEFB	0H
MSGIN1	DEFM	' The Nestirons are attacking the Earth !  '
	DEFB	0H
MSGIN2	DEFM	'Mission Objective: Kill all Nestirons, and stay alive.'
	DEFB	0H
MSGIN3	DEFM	'Use the left and right arrows or < > to move left and right,'
	DEFB	0H
MSGIN4	DEFM	'and shoot with the space bar, or "F" or "@".  You must shoot'
	DEFB	0H
MSGIN5	DEFM	'each Nestiron twice - once to wake it, and once to kill it.  '
	DEFB	0H
MSGIN6	DEFM	'          MAY THE POWER BE FOUND WITHIN YOU.         '
	DEFB	0H
MSGIN7	DEFM	'   Pod    Eyes  Fighter  Drone   Armor  Executioner  Cyclops'
	DEFB	0H
MSGIN8	DEFM	'   '
	DEFW	0BFAEH
	DEFW	84BFH
	DEFM	'   '
	DEFW	99A6H
	DEFW	84B3H
	DEFM	'    '
	DEFW	999DH
	DEFB	95H
	DEFM	'     '
	DEFW	0BBBBH
	DEFB	91H
	DEFM	'     '
	DEFW	0AEBBH
	DEFB	91H
	DEFM	'       '
	DEFW	0B7B7H
	DEFB	95H
	DEFM	'        '
	DEFW	095B7H
	DEFB	0H
MSGINA	DEFM	'    10     50     60      80      100       150       200'
	DEFB	0H
NUMHI1	DEFM	'000000'
NUMHI2	DEFM	'000000'
NUMHI3	DEFM	'000000'
NUMHI4	DEFM	'000000'
NUMHI5	DEFM	'000000'
HINM1	DEFM	'1.         '
	DEFB	0H
HINM2	DEFM	'2.         '
	DEFB	0H
HINM3	DEFM	'3.         '
	DEFB	0H
HINM4	DEFM	'4.         '
	DEFB	0H
HINM5	DEFM	'5.         '
	DEFB	0H
POPPL2	LD	A,1
	LD	(NOPLAY),A
	LD	A,0
	LD	(DEMOF),A
	JP	MOVE
POPPL1	LD	A,0
	LD	(NOPLAY),A
	LD	(DEMOF),A
	JP	MOVE
POPDEM	LD	A,1
	LD	(NOPLAY),A
	LD	(DEMOF),A
	JP	MOVE
	END	FIXSTR

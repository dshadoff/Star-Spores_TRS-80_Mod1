	ORG	54A0H
SQNFG8	EQU	78C0H
VCTRHI	EQU	7880H
SCREEN	EQU	5000H
SHPTBL	EQU	7A6FH
SCRN	EQU	50H
SCORE1	EQU	SCREEN+6
SCORE2	EQU	SCREEN+39H
TBSTF1	EQU	7B97H
COUNT1	EQU	7B85H
SEQUN4	EQU	7C7CH
SEQUN3	EQU	7C4BH
SEQUN2	EQU	7C0EH
SEQUN1	EQU	7BEFH
BLNKGY	EQU	7CC0H
TBPTS	EQU	7770H
RNDPTS	EQU	779CH
TABL1	EQU	77ACH
XPLODE	EQU	6E40H
PLYMS	EQU	70DDH
PLYNO	EQU	730AH
PLYNO1	EQU	730BH
GYTYP1	EQU	730CH
NUSHP1	EQU	730DH
SPFX	EQU	7114H
LEVEL	EQU	730FH
LEVELS	EQU	7242H
RET	EQU	71D3H
TITLEP	EQU	6780H
ENTRY	DI
	LD	A,3AH
	LD	(SHPPOS),A
	LD	(SHPPS1),A
	CALL	SPFX
	LD	H,0
	LD	A,R
	LD	L,A
	LD	(CONTER),HL
	LD	A,R
	LD	(RNDNO),A
	AND	7
	INC	A
	LD	(RNDPTS),A
	LD	HL,SCREEN+3FFH
	LD	(SHTLOC),HL
	LD	HL,3C00H
	LD	DE,3C01H
	LD	BC,03FFH
	LD	(HL),80H
	LDIR
	LD	HL,SCREEN
	LD	DE,SCREEN+1
	LD	BC,3FFH
	LD	(HL),80H
	LDIR
	LD	A,95H
	LD	(SCREEN+11H),A
	LD	A,0AAH
	LD	(SCREEN+2EH),A
	LD	HL,TIMMSG
	LD	DE,3C12H
	CALL	PRNSTR
	LD	A,3AH
	LD	(SHPPOS),A
	LD	HL,SHPTBL+1
	LD	(SHPON),HL
	LD	DE,3C01H
	LD	HL,RSTSCR
	CALL	PRNSTR
	LD	HL,RSTSCR
	LD	DE,3C34H
	CALL	PRNSTR
	LD	A,'0'
	LD	(SCRBF1),A
	LD	(SCRBF2),A
	LD	A,(NOPLAY)
	OR	A
	JR	NZ,TWOPLY
	LD	A,'0'
	LD	(SCORE2+5),A
TWOPLY	LD	A,1
	LD	(GYTYP),A
	LD	(GYTYP1),A
	LD	A,3
	LD	(PLYNO1),A
	LD	(PLYNO),A
	XOR	A
	LD	(P1OR2F),A
	LD	(NUSHIP),A
	LD	(NUSHP1),A
	LD	HL,TIMLIN
	LD	(HL),46
	CALL	TMLNPT
	LD	HL,SCREEN
	LD	DE,3C00H
	LD	BC,40H
	LDIR
	LD	A,1
	LD	(SHOOTF),A
	CALL	MOVSHP
	CALL	PLYMS
SHEET	LD	SP,4F00H
	LD	A,1
	LD	(KLTIME),A
	LD	HL,TIMLIN
	LD	(HL),46
	CALL	TMLNPT
	LD	HL,TIMECN
	LD	(HL),0
	XOR	A
	LD	B,6
	LD	HL,BPNTS
	LD	(HL),A
	INC	HL
	DJNZ	$-2
	LD	(CZAPNO),A
	LD	(BPNTFL),A
	LD	(TMCRTC),A
	LD	(DJNZL),A
	LD	(KBRD),A
	LD	(DEMMOV),A
	LD	(CNTKIL),A
	CALL	SETARR
	CALL	LEVELS
	XOR	A
	LD	(SHOT1F),A
	LD	(SHOT2F),A
	LD	(SHOOTF),A
	LD	HL,SCPTTB
	LD	B,39
XSCPTB	LD	(HL),A
	INC	HL
	DJNZ	XSCPTB
	LD	HL,SHTTBL
	LD	B,46
	LD	(HL),A
	INC	HL
	DJNZ	$-2
	LD	IX,COUNT1
	LD	IY,TBSTF1
	LD	B,8
HIYA	LD	HL,3C83H
	LD	A,0
	LD	(IY+0),A
	LD	(IY+3),L
	LD	(IY+4),H
	LD	(IY+5),L
	LD	(IY+6),H
	LD	(IY+7),A
	LD	HL,(CONTER)
	LD	A,(RNDNO)
	XOR	(HL)
	LD	(RNDNO),A
	AND	7FH
	INC	A
	CP	119
	JR	C,XFIXED
	SUB	60
XFIXED	INC	HL
	LD	(IY+2),A
	LD	A,(RNDNO)
	XOR	(HL)
	LD	(RNDNO),A
	AND	0FH
	INC	A
	CP	15
	JR	C,YFIXED
	SUB	7
YFIXED	INC	HL
	LD	(IY+1),A
	INC	HL
	LD	A,(RNDNO)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	LD	(RNDNO),A
	CP	0C0H
	JR	C,PLCFIX
	SUB	0C0H
PLCFIX	LD	(CONTER),HL
	LD	HL,SEQUN1
	LD	D,0
	LD	E,A
	ADD	HL,DE
	LD	A,(HL)
	CP	0AH
	JR	C,CNTFIX
	INC	HL
CNTFIX	LD	(IX+0),L
	LD	(IX+1),H
	INC	IX
	INC	IX
	PUSH	BC
	LD	B,8
BACK	INC	IY
	DJNZ	BACK
	POP	BC
	DJNZ	HIYA
;------------------------------
RERUN	CALL	TIME
	LD	HL,(CONTER)
	INC	HL
	LD	(CONTER),HL
	LD	A,H
	CP	2FH
	JR	NZ,CNTINU
	LD	H,0
	LD	(CONTER),HL
CNTINU	LD	A,(RNDNO)
	XOR	(HL)
	LD	(RNDNO),A
	LD	A,L
	SRL	A
	JR	NC,CALMVS
	CALL	RND1
CALMVS	CALL	MOVSHP
	CALL	UPDSHT
	LD	BC,SPEED
SPEEDX	EQU	$-2
	CALL	60H
	JR	RERUN
;------------------------------
TIME	LD	A,(CNTDED)
	CP	8
	JR	NZ,LDHLTC
	CALL	TMLNPT
	LD	A,(SHTFIN)
	OR	A
	RET	NZ
	LD	A,(NUMBRS)
	OR	A
	RET	NZ
	LD	A,(DJNZL)
	OR	A
	JR	Z,SDJNZF
	DEC	A
	LD	(DJNZL),A
	JP	Z,BONUSP
	RET
SDJNZF	LD	A,40H
	LD	(DJNZL),A
	RET
LDHLTC	LD	HL,TIMECN
	INC	(HL)
	LD	A,(HL)
	CP	20H
	JR	NZ,CKCRTC
	LD	(HL),0
	LD	A,(TIMLIN)
	ADD	A,20
	LD	L,A
	LD	H,0
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	CALL	SOUND
	CALL	SOUND
	CALL	SOUND
	LD	HL,TIMLIN
	DEC	(HL)
	JR	NZ,CKCRTC
	LD	A,80H
	LD	(SCREEN+17H),A
	JP	ELABRX
CKCRTC	LD	A,(TMCRTC)
	OR	A
	JR	Z,NOCRTC
	DEC	A
	LD	(TMCRTC),A
	CP	5
	JR	NC,BLNKT1
	CALL	TMLNOT
	JR	BLNKET
BLNKT1	CALL	TMLNPT
BLNKET	LD	HL,10H
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,8
	PUSH	BC
	CALL	SOUND
	POP	BC
	DJNZ	$-5
	RET
NOCRTC	LD	A,(TIMLIN)
	CP	7
	JP	NC,TMLNPT
	LD	A,8
	LD	(TMCRTC),A
	JP	TMLNPT
TMLNOT	LD	B,8
	LD	HL,SCREEN+17H
	LD	A,80H
TMLOT1	LD	(HL),A
	INC	HL
	DJNZ	TMLOT1
	RET
BONUSP	LD	HL,MSGBNS
	LD	DE,795BH-SCREEN
	CALL	PRNSTR
	LD	HL,TIMLIN
	INC	(HL)
BONSLP	DEC	(HL)
	CALL	CLICK
	JR	Z,DECFIN
	PUSH	HL
	LD	HL,BPOINT
	LD	DE,BPNTS
	LD	B,6
	LD	A,(DE)
	ADD	A,(HL)
	LD	(DE),A
	INC	HL
	INC	DE
	DJNZ	$-5
CRYCRY	LD	HL,BPNTS
	LD	B,6
CRYCR2	LD	A,(HL)
	CP	10
	JR	C,CRYCR1
	SUB	10
	LD	(HL),A
	INC	HL
	INC	(HL)
	DEC	HL
CRYCR1	INC	HL
	DJNZ	CRYCR2
	LD	HL,BPNTS+5
	LD	DE,3D56H
	LD	B,6
CRYCR3	LD	A,(BPNTFL)
	OR	(HL)
	JR	Z,CRYCR4
	LD	A,(HL)
	ADD	A,30H
	LD	(DE),A
	LD	A,1
	LD	(BPNTFL),A
CRYCR4	DEC	HL
	INC	DE
	DJNZ	CRYCR3
	POP	HL
	XOR	A
	LD	(BPNTFL),A
	JR	BONSLP
DECFIN	LD	BC,3000H
	CALL	60H
	LD	HL,BPNTS
	LD	B,6
	LD	A,(P1OR2F)
	LD	DE,SCORE1
	OR	A
	JR	Z,DECFN2-2
	LD	DE,SCORE2
	PUSH	HL
	PUSH	DE
DECFN2	LD	C,(HL)
	LD	A,(DE)
	ADD	A,C
	LD	(DE),A
	DEC	DE
	INC	HL
	DJNZ	DECFN2
	LD	B,6
	POP	DE
	POP	HL
CRYCR6	LD	A,(DE)
	CP	'9'+1
	JR	C,CRYCR7
	SUB	10
	LD	(DE),A
	DEC	DE
	LD	A,(DE)
	INC	A
	LD	(DE),A
	DJNZ	CRYCR6
	JR	CRYCR8
CRYCR7	DEC	DE
	DJNZ	CRYCR6
CRYCR8	LD	A,(SCORE1-4)
	LD	HL,SCRBF1
	CP	(HL)
	LD	(HL),A
	JR	Z,NOBNS5
	LD	HL,SCORE1+5
	LD	A,32
	LD	(NUSHIP),A
	INC	(HL)
	LD	A,(HL)
	CP	'9'+1
	JR	NZ,NOBNS5
	SUB	10
	LD	(HL),A
	DEC	HL
	INC	(HL)
NOBNS5	LD	A,(SCORE2-4)
	LD	HL,SCRBF2
	CP	(HL)
	LD	(HL),A
	JR	Z,NOBNS6
	LD	HL,SCORE2+5
	LD	A,32
	LD	(NUSHIP),A
	INC	(HL)
	LD	A,(HL)
	CP	'9'+1
	JR	NZ,NOBNS6
	SUB	10
	LD	(HL),A
	DEC	HL
	INC	(HL)
NOBNS6	LD	BC,3000H
	CALL	60H
	LD	HL,TIMLIN
DECFN1	INC	(HL)
	CALL	CLICK
	LD	A,(HL)
	CP	46
	JR	NZ,DECFN1
	LD	BC,3800H
	CALL	60H
	LD	HL,3D40H
	LD	DE,3D41H
	LD	BC,40H
	LD	(HL),80H
	LDIR
	LD	A,(GYTYP)
	INC	A
	LD	(GYTYP),A
	CP	7
	JP	NZ,SHEET
	LD	A,1
	LD	(GYTYP),A
	JP	SHEET
CLICK	PUSH	AF
	PUSH	HL
	LD	A,(3840H)
	AND	6
	CP	6
	JR	NZ,CLICK2
	POP	HL
	POP	AF
	EX	(SP),HL
	POP	HL
	JP	ED00H		;Aborted.
CLICK2	LD	A,(TIMLIN)
	SRL	A
	NEG
	ADD	A,50H
	LD	L,A
	LD	H,0
	LD	DE,28H
	LD	B,14H
BSNDLP	PUSH	BC
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	CALL	SOUND
	LD	(SOUND1),DE
	LD	(SOUND2),DE
	CALL	SOUND
	POP	BC
	DJNZ	BSNDLP
	CALL	TMLNPT
	LD	HL,SCREEN	
	LD	DE,3C00h
	LD	BC,40H
	LDIR
	POP	HL
	POP	AF
	RET
ELABRX	LD	IY,TBSTF1
	LD	B,8
	XOR	A
	LD	(KLTIME),A
AGAINB	PUSH	BC
	LD	A,(IY+0)
	AND	0C0H
	CP	80H
	JR	Z,AGAINA
	LD	A,(GYTYP)
	CALL	NOBNS3
	LD	(IY+7),8FH
AGAINA	LD	B,8
	INC	IY
	DJNZ	$-2
	POP	BC
	DJNZ	AGAINB
	CALL	RND1
	LD	IY,TBSTF1
	LD	IX,SHTTBL
	LD	B,8
AGAINC	PUSH	BC
	LD	A,(IY+0)
	AND	0C0H
	CP	80H
	JR	Z,AGAIND
	LD	A,5
	LD	(CZAPNO),A
	LD	(IX+0),90H
	LD	A,(IY+1)
	ADD	A,6
	LD	(IX+1),A
	LD	B,(IY+2)
	LD	C,3
	LD	A,(GYTYP)
	CP	6
	JR	Z,NCA
	CP	1
	JR	NZ,NCB
	INC	C
NCB	INC	C
NCA	LD	A,B
	ADD	A,C
	LD	(IX+2),A
	LD	A,(CNTKIL)
	INC	A
	LD	(CNTKIL),A
	LD	B,5
	INC	IX
	DJNZ	$-2
AGAIND	LD	B,8
	INC	IY
	DJNZ	$-2
	POP	BC
	DJNZ	AGAINC
	LD	A,(CNTKIL)
	OR	A
	JR	NZ,AGAINE
	LD	(IX+0),90H
	LD	(IX+1),0
	LD	(IX+2),0
AGAINE	CALL	UPDSHT
	POP	HL
	JP	XPLODE
;============================
UPDSHT	LD	IY,SHTTBL
	LD	B,8
	LD	A,(BULET1)
	XOR	1
	LD	(BULET1),A
	XOR	A
	LD	(SHTFIN),A
	LD	(XBLSNO),A
UPDST1	PUSH	BC
	BIT	7,(IY+0)
	JP	Z,NXTDO1
	LD	A,(SHTFIN)
	INC	A
	LD	(SHTFIN),A
	BIT	6,(IY+0)
	JR	NZ,BULLET
	BIT	5,(IY+0)
	JR	NZ,LINE1
ZOT	LD	A,(SHPPS1)
	ADD	A,6
	LD	(IY+4),A
	LD	(IY+3),45
	XOR	A
	LD	(STORS1),A
	CALL	LNEPUT
	CALL	REVERS
	CALL	REVERS
	LD	A,1
	LD	(STORS1),A
	CALL	LNEPUT
	LD	A,(CZAPNO)
	INC	A
	LD	(CZAPNO),A
ZOT16	RES	7,(IY+0)
	LD	A,(SHPPS1)
	LD	(SHPPOS),A
	CALL	FINTO1
	JP	NXTDO1
BULLET	LD	A,(BULET1)
	OR	A
	JR	NZ,NEXTDO
	CALL	UPTDOT
	BIT	3,(IY+0)
	CALL	NZ,DECX1
	BIT	2,(IY+0)
	CALL	NZ,INCX2
	CALL	INCY1
	BIT	7,(IY+0)
	JR	Z,NEXTDO
	CALL	PUTDOT
	JR	NEXTDO
LINE1	LD	A,(XBLSNO)
	INC	A
	LD	(XBLSNO),A
	CALL	INCY1
	SET	7,(IY+0)
	CP	2FH
	JR	Z,LINE2
	SUB	(IY+3)
	CP	10
	CALL	NC,INCY2
LINE3	XOR	A
	LD	(STORS1),A
	LD	A,(IY+2)
	LD	(IY+4),A
	CALL	LNEPT1
	DEC	(IY+3)
	LD	A,1
	LD	(STORS1),A
	LD	A,(IY+3)
	LD	(STARTY+1),A
	LD	A,(IY+2)
	LD	(STARTX+1),A
	XOR	A
	LD	(STARTX),A
	LD	(STARTY),A
	CALL	SET
	INC	(IY+3)
	JR	LINE4
LINE2	CALL	INCY2
	JR	LINE3
LINE4	LD	A,(IY+3)
	CP	2FH
	JR	NZ,NEXTDO
	CALL	UPTDOT
	RES	7,(IY+0)
NEXTDO	LD	A,(IY+1)
	CP	45
	JR	C,NXTDO1
	CP	47
	LD	A,1
	LD	(CPHTLO),A
	LD	A,12
	LD	(CPHTHI),A
	JR	Z,NXTDO2
	CP	46
	LD	A,0
	LD	(CPHTLO),A
	LD	A,10
	LD	(CPHTHI),A
	JR	Z,NXTDO2
	LD	A,(SHPPS1)
	ADD	A,2
	CP	(IY+2)
	JR	Z,NXTDO5
	ADD	A,7
	CP	(IY+2)
	JR	Z,NXTDO5
	JR	NXTDO1
NXTDO2	LD	A,(SHPPS1)
	SUB	1
CPHTLO	EQU	$-1
	CP	(IY+2)
	JR	NC,NXTDO1
	ADD	A,12
CPHTHI	EQU	$-1
	CP	(IY+2)
	JR	C,NXTDO1
NXTDO5	LD	A,5
	LD	(CZAPNO),A
NXTDO1	LD	B,5
	INC	IY
	DJNZ	$-2
	POP	BC
	DEC	B
	JP	NZ,UPDST1
	LD	A,(CZAPNO)
	CP	5
	JP	NC,XPLODE
	LD	DE,(SHPOFF)
	LD	HL,(SHPON)
CPXPLD	LD	A,(HL)
	OR	A
	JR	Z,ADJUST
	LD	A,(DE)
	AND	(HL)
	CP	(HL)
	JP	NZ,XPLODE
	INC	HL
	INC	DE
	JR	CPXPLD
ADJUST	LD	A,(3804H)
	AND	1
	JR	Z,CNTMOV
STOP	LD	A,(3840H)
	AND	1
	JR	Z,STOP
CNTMOV	LD	A,(XBLSNO)
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	LD	D,0
	JR	NC,NOINCD
	INC	D
NOINCD	LD	E,A
	LD	HL,(SPEED)
	AND	A
	SBC	HL,DE
	LD	(SPEEDX),HL
	RET
INCX2	INC	(IY+2)
	LD	A,(IY+2)
	CP	7FH
	JR	NC,BADX
	RET
DECX1	DEC	(IY+2)
	LD	A,(IY+2)
	JR	Z,BADX
	JR	C,BADX
	RET
BADX	RES	7,(IY+0)
	RET
INCY1	INC	(IY+1)
	LD	A,(IY+1)
	CP	30H
	RET	C
	LD	A,2FH
	LD	(IY+1),A
	JR	BADX
INCY2	INC	(IY+3)
	LD	A,(IY+3)
	CP	30H
	RET	C
	LD	A,2FH
	LD	(IY+3),A
	JR	BADX
PUTDOT	XOR	A
	JR	UPTDT1
UPTDOT	LD	A,1
UPTDT1	LD	(STORS1),A
	LD	A,(IY+1)
	LD	(STARTY+1),A
	LD	A,(IY+2)
	LD	(STARTX+1),A
	XOR	A
	LD	(STARTX),A
	LD	(STARTY),A
	JP	SET
LNEPUT	LD	H,(IY+1)
	LD	L,(IY+2)
	LD	D,(IY+3)
	LD	E,(IY+4)
	AND	A
	PUSH	HL
	SBC	HL,DE
	POP	HL
	LD	A,H
	LD	(STARTY+1),A
	LD	A,L
	LD	(STARTX+1),A
	LD	A,D
	LD	(ENDY+1),A
	LD	A,E
	LD	(ENDX+1),A
	LD	A,0
	LD	(STARTX),A
	LD	(ENDX),A
	LD	(STARTY),A
	LD	(ENDY),A
	JP	Z,SET
	JP	LINE
LNEPT1	XOR	A
	LD	(STARTX),A
	LD	(ENDX),A
	LD	(STARTY),A
	LD	(ENDY),A
	LD	A,(IY+1)
	LD	(STARTY+1),A
	LD	A,(IY+2)
	LD	(STARTX+1),A
	LD	A,(IY+3)
	LD	(ENDY+1),A
LNEPT2	CALL	SET
	LD	A,(STARTY+1)
	LD	B,A
	LD	A,(ENDY+1)
	CP	B
	RET	Z
	DEC	B
	LD	A,B
	LD	(STARTY+1),A
	JR	LNEPT2
REVERS	LD	HL,3C40H
	LD	BC,3C0H
REVRS1	LD	A,(HL)
	XOR	3FH
	LD	(HL),A
	INC	HL
	DEC	BC
	LD	A,C
	OR	B
	JR	NZ,REVRS1
	RET
;===========================
RND1	LD	B,8
	LD	IX,COUNT1
	LD	IY,TBSTF1
RANDOM	PUSH	BC
	LD	A,(IY+0)
	BIT	7,A
	JR	NZ,FIGGER
	AND	38H
	JP	NZ,AGGRES
RANDM1	LD	L,(IX)
	LD	H,(IX+1)
	INC	HL
	LD	A,(HL)
	CP	8
	JR	C,PUTBAK
	LD	E,A
	LD	D,0
	OR	A
	SBC	HL,DE
PUTBAK	LD	(IX),L
	LD	(IX+1),H
	LD	A,(HL)
	OR	A
	JR	NZ,CHKONE
	CALL	DECY
CHKONE	DEC	A
	JR	NZ,CHKTWO
	CALL	DECY
	CALL	INCX
CHKTWO	DEC	A
	JR	NZ,CHKTHR
	CALL	INCX
CHKTHR	DEC	A
	JR	NZ,CHKFOR
	CALL	INCX
	CALL	INCY
CHKFOR	DEC	A
	JR	NZ,CHKFIV
	CALL	INCY
CHKFIV	DEC	A
	JR	NZ,CHKSIX
	CALL	INCY
	CALL	DECX
CHKSIX	DEC	A
	JR	NZ,CHKSEV
	CALL	DECX
CHKSEV	DEC	A
	JR	NZ,FIGGER
	CALL	DECX
	CALL	DECY
FIGGER	LD	L,(IY+5)
	LD	H,(IY+6)
	LD	(IY+3),L
	LD	(IY+4),H
	LD	A,(IY+2)
	LD	E,A
	SRL	E
	LD	D,0
	JR	NC,SET11
	INC	D
SET11	LD	A,(IY+1)
	LD	B,0FFH
SET20	INC	B
	SUB	3
	JP	P,SET20
SET25	ADD	A,3
	RLCA
	ADD	A,D
	LD	H,A
	LD	A,(IY+0)
	AND	0F8H
	OR	H
	LD	(IY+0),A
	LD	L,B
	LD	H,0
	LD	B,6
SET30	ADD	HL,HL
	DJNZ	SET30
	LD	D,0
	ADD	HL,DE
	LD	DE,3C41H
	ADD	HL,DE
	LD	(IY+5),L
	LD	(IY+6),H
	INC	IX
	INC	IX
	LD	B,8
BAK1	INC	IY
	DJNZ	BAK1
	POP	BC
	DEC	B
	JP	NZ,RANDOM
	LD	B,8
	LD	IY,TBSTF1
	LD	IX,COUNT1
BLNKNG	LD	HL,BLNKGY
	LD	E,(IY+3)
	LD	D,(IY+4)
	CALL	PRNSTR
	INC	IX
	INC	IX
	PUSH	BC
	LD	B,8
BAC2	INC	IY
	DJNZ	BAC2
	POP	BC
	DJNZ	BLNKNG
	LD	A,(KLTIME)
	OR	A
	JP	Z,PRNGYS
;*************************
SHOOT	LD	HL,(SHTLOC)
	LD	(HL),80H
	PUSH	HL
	POP	IX
	LD	(IX+3),80H
	LD	(IX+4),80H
	PUSH	HL
	PUSH	HL
	LD	DE,SCREEN-3C00H
	SBC	HL,DE
	EX	DE,HL
	POP	HL
	LD	BC,5
	LDIR
	POP	HL
	LD	A,(SHOT1F)
	LD	B,A
	LD	A,(SHOT2F)
	OR	B
	JP	Z,CKSHUT
	LD	DE,40H
	AND	A
	SBC	HL,DE
	LD	A,H
	CP	SCRN
	JR	NZ,AA011
	LD	A,L
	CP	40H
	JR	C,RSHFLG
AA011	LD	(SHTLOC),HL
PUTSHT	LD	A,(SHTPOS)
	LD	B,0
	SRL	A
	JR	NC,GOIN
	INC	B
GOIN	LD	A,81H
	ADD	A,B
	PUSH	AF
	LD	A,(SHOT1F)
	OR	A
	JR	Z,KEPGO1
	POP	AF
	LD	(HL),A
	JR	KEPGO2
KEPGO1	POP	AF
KEPGO2	INC	B
	INC	B
	INC	B
AA012	INC	HL
	DJNZ	AA012
	XOR	3
	PUSH	AF
	LD	A,(SHOT2F)
	OR	A
	JR	Z,KEPGO3
	POP	AF
	LD	(HL),A
	JR	KEPGO4
KEPGO3	POP	AF
KEPGO4	LD	HL,(SHTLOC)
	LD	DE,SCREEN-3C00H
	AND	A
	SBC	HL,DE
	EX	DE,HL
	LD	HL,(SHTLOC)
	LD	BC,5
	LDIR
	JP	PRNGYS
RSHFLG	XOR	A
	LD	(SHOT1F),A
	LD	(SHOT2F),A
	LD	HL,(SHTLOC)
	LD	DE,SCREEN-3C00H
	AND	A
	SBC	HL,DE
	LD	E,L
	LD	D,H
	INC	DE
	LD	BC,4
	LD	(HL),80H
	LDIR
	JR	PRNGYS
CKSHUT	LD	A,(KBRD)
	AND	16
	JR	NZ,CONTNU
	XOR	A
	LD	(SHOOTF),A
	JR	PRNGYS
CONTNU	LD	A,(SHOOTF)
	OR	A
	JR	Z,CNTNU1
	JR	PRNGYS
CNTNU1	LD	A,1
	LD	(SHOT1F),A
	LD	(SHOT2F),A
	LD	(SHOOTF),A
	LD	A,(SHPPOS)
	ADD	A,2
	LD	(SHTPOS),A
	SRL	A
	LD	E,A
	LD	D,0
	LD	HL,SCREEN+380H
	ADD	HL,DE
	LD	(SHTLOC),HL
	LD	HL,20H
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,9
HERE	LD	HL,(SOUND1)
	DEC	HL
	DEC	HL
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	PUSH	BC
	CALL	SOUND
	CALL	SOUND
	POP	BC
	DJNZ	HERE
	POP	HL
	LD	HL,RERUN
	PUSH 	HL
	LD	HL,(SHTLOC)
	CALL	PUTSHT
	CALL	MOVSHP
PRNGYS	LD	B,8
	LD	IY,TBSTF1
	LD	IX,COUNT1
	XOR	A
	LD	(CNTDED),A
PRNGUY	PUSH	BC
	LD	E,(IY+5)
	LD	D,(IY+6)
	PUSH	DE
	CALL	CHKDED
	POP	DE
	LD	HL,TABL1-2
	LD	A,(IY+0)
	LD	B,A
	BIT	7,A
	JR	Z,$+10
	LD	A,(CNTDED)
	INC	A
	LD	(CNTDED),A
	LD	A,B
	AND	0C0H
	CP	80H
	JR	Z,INVALD
	LD	A,B
	AND	78H
	CP	38H
	JR	NZ,LDAB
	LD	A,B
	AND	87H
	LD	B,A
	LD	A,(GYTYP)
	AND	7
	SLA	A
	SLA	A
	SLA	A
	OR	B
	LD	B,A
LDAB	LD	A,B
	AND	7FH
	LD	B,A
	INC	B
AGN	INC	HL
	INC	HL
	DJNZ	AGN
	PUSH	IX
	PUSH	HL
	POP	IX
	LD	L,(IX)
	LD	H,(IX+1)
	POP	IX
	PUSH	DE
	CALL	PRNSTR
	POP	DE
INVALD	LD	HL,66
	EX	DE,HL
	AND	A
	SBC	HL,DE
	EX	DE,HL
	LD	HL,SCREEN-3C00H
	ADD	HL,DE
	LD	BC,7
	LDIR
	LD	BC,57
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	LD	BC,7
	LDIR
	LD	BC,57
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	LD	BC,7
	LDIR
	LD	A,(IY+0)
	LD	B,A
	AND	40H
	JR	Z,NEXPLD
	LD	A,B
	AND	38H
	ADD	A,8
	PUSH	BC
	LD	B,0EH
HISSSS	PUSH	AF
	PUSH	BC
	SRL	A
	LD	B,A
	DEC	B
	LD	HL,(CONTER)
	LD	A,(RNDNO)
	XOR	(HL)
	INC	HL
	LD	(RNDNO),A
	PUSH	HL
	AND	B
	ADD	A,5
	LD	H,0
	LD	L,A
	LD	(SOUND1),HL
	POP	HL
	LD	A,(RNDNO)
	XOR	(HL)
	LD	(RNDNO),A
	INC	HL
	LD	(CONTER),HL
	AND	B
	ADD	A,5
	LD	L,A
	LD	H,0
	LD	(SOUND2),HL
	CALL	SOUND
	POP	BC
	POP	AF
	DJNZ	HISSSS
	POP	BC
	CP	20H
	JR	NZ,NOXPLD
	LD	A,B
	AND	87H
	LD	(IY+0),A
	JR	NEXPLD
NOXPLD	LD	A,B
	ADD	A,8
	LD	(IY+0),A
NEXPLD	INC	IX
	INC	IX
	LD	B,8
BAC3	INC	IY
	DJNZ	BAC3
	POP	BC
	DEC	B
	JP	NZ,PRNGUY
	LD	HL,SCREEN
	LD	DE,3C00H
	LD	BC,40H
	LDIR
	RET
;******************
SOUND	LD	A,1
	OUT	(0FFH),A
	LD	BC,30H
SOUND1	EQU	$-2
	CALL	60H
	LD	A,2
	OUT	(0FFH),A
	LD	BC,30H
SOUND2	EQU	$-2
	CALL	60H
	RET
;******************
CHKDED	LD	A,(IY+0)
	AND	0C0H
	RET	NZ
	LD	HL,SCREEN-3C00H
	ADD	HL,DE
	EX	DE,HL
CHKY	LD	HL,(SHTLOC)
	EX	DE,HL
	LD	A,E
	AND	0C0H
	LD	E,A
	LD	A,L
	AND	0C0H
	LD	L,A
	LD	A,H
	CP	D
	JR	NZ,TTAGN
	LD	A,L
	CP	E
	JR	Z,CHKX
TTAGN	LD	BC,40H
	ADD	HL,BC
	LD	A,H
	CP	D
	RET	NZ
	LD	A,L
	CP	E
	RET	NZ
CHKX	LD	A,(IY+0)
	AND	38H
	LD	B,7
	JR	Z,CHKXLP
	SUB	8
	JR	Z,CHKXLP
	LD	B,3
	SUB	28H
	JR	Z,CHKXLP
	LD	B,5
CHKXLP	LD	A,(IY+2)
	LD	C,A
	LD	A,(SHTPOS)
	DEC	A
	DEC	A
CKXLP1	CP	C
	JR	Z,HIT
	ADD	A,7
	CP	C
	JR	Z,HIT1
	SUB	7
	INC	C
	DJNZ	CKXLP1
	RET
HIT	LD	A,(SHOT1F)
	OR	A
	RET	Z
	XOR	A
	LD	(SHOT1F),A
	JR	HIT2
HIT1	LD	A,(SHOT2F)
	OR	A
	RET	Z
	XOR	A
	LD	(SHOT2F),A
HIT2	LD	A,(IY+0)
	SRL	A
	SRL	A
	SRL	A
	AND	7
	LD	B,A
	CP	7
	JR	NZ,LDHLTB
	PUSH	IY
	LD	IY,SCPTTB
TRANTR	BIT	7,(IY+0)
	JR	Z,GOTIT
	PUSH	BC
	LD	B,4
	INC	IY
	DJNZ	$-2
	POP	BC
	JR	TRANTR
GOTIT	LD	(IY+0),9FH
	LD	A,(RNDPTS)
	LD	(IY+1),A
	EX	(SP),IY
	LD	H,(IY+6)
	LD	L,(IY+5)
	EX	(SP),IY
	LD	(IY+3),H
	LD	(IY+2),L
	POP	IY
LDHLTB	LD	HL,TBPTS-6
	LD	DE,6
	INC	B
GTPTTB	ADD	HL,DE
	DJNZ	GTPTTB
	LD	B,6
	LD	A,(P1OR2F)
	LD	DE,SCORE1
	OR	A
	JR	Z,SCRADD-2
	LD	DE,SCORE2
	PUSH	HL
	PUSH	DE
SCRADD	LD	C,(HL)
	LD	A,(DE)
	ADD	A,C
	LD	(DE),A
	DEC	DE
	INC	HL
	DJNZ	SCRADD
	LD	B,6
	POP	DE
	POP	HL
CARRY	LD	A,(DE)
	CP	'9'+1
	JR	C,NOCARY
	SUB	10
	LD	(DE),A
	DEC	DE
	LD	A,(DE)
	INC	A
	LD	(DE),A
	DJNZ	CARRY
	JR	CRYOVR
NOCARY	DEC	DE
	DJNZ	CARRY
CRYOVR	LD	A,(SCORE1-4)
	LD	HL,SCRBF1
	CP	(HL)
	LD	(HL),A
	JR	Z,NOBNS1
	LD	HL,SCORE1+5
	LD	A,32
	LD	(NUSHIP),A
	INC	(HL)
	LD	A,(HL)
	CP	'9'+1
	JR	NZ,NOBNS1
	SUB	10
	LD	(HL),A
	DEC	HL
	INC	(HL)
NOBNS1	LD	A,(SCORE2-4)
	LD	HL,SCRBF2
	CP	(HL)
	LD	(HL),A
	JR	Z,NOBNS2
	LD	HL,SCORE2+5
	LD	A,32
	LD	(NUSHIP),A
	INC	(HL)
	LD	A,(HL)
	CP	'9'+1
	JR	NZ,NOBNS2
	SUB	10
	LD	(HL),A
	DEC	HL
	INC	(HL)
NOBNS2	LD	A,(IY+0)
	AND	38H
	JR	NZ,EARMRK
	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	7
	LD	A,7
	JR	Z,NOBNS3
	LD	A,(GYTYP)
NOBNS3	AND	7
	LD	C,(IY+2)
	CP	1
	JR	Z,INCX0
	CP	6
	JR	NZ,INCX1
	INC	C
INCX1	INC	C
INCX0	LD	(IY+2),C
	SLA	A
	SLA	A
	SLA	A
	LD	B,A
	LD	A,(IY+0)
	AND	7
	OR	B
	LD	(IY+0),A
	LD	A,40H
	SUB	B
	LD	L,A
	LD	H,0
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,9
BEEEEP	PUSH	BC
	CALL	SOUND
	POP	BC
	DJNZ	BEEEEP
	RET
EARMRK	LD	A,(IY+0)
	OR	0C0H
	AND	0C7H
	LD	(IY+0),A
	LD	HL,(CONTER)
	LD	A,(RNDNO)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	7
	INC	A
	LD	(RNDPTS),A
	RET
;**********************
MOVSHP	LD	A,(NUSHIP)
	OR	A
	JR	Z,MVSHP9
	DEC	A
	LD	(NUSHIP),A
	BIT	2,A
	JR	Z,MVSHP9
	LD	HL,0AH
	LD	(SOUND1),HL
	LD	(SOUND2),HL
	LD	B,14H
NSHPSN	PUSH	BC
	CALL	SOUND
	POP	BC
	DJNZ	NSHPSN
MVSHP9	LD	IY,SCPTTB
	LD	B,8
	XOR	A
	LD	(NUMBRS),A
PTSCSC	PUSH	BC
	LD	A,(IY+0)
	BIT	7,A
	JR	Z,FINITO
	PUSH	AF
	LD	A,(NUMBRS)
	INC	A
	LD	(NUMBRS),A
	POP	AF
	AND	1FH
	DEC	A
	OR	80H
	LD	(IY+0),A
	AND	1FH
	JR	Z,ERASIT
	CP	26
	JR	NC,FINITO
	JR	PRINIT
ERASIT	RES	7,(IY+0)
	LD	H,(IY+3)
	LD	L,(IY+2)
	LD	(HL),80H
	INC	HL
	LD	(HL),80H
	INC	HL
	LD	(HL),80H
	JR	FINITO
PRINIT	LD	A,(IY+1)
	ADD	A,'0'
	LD	H,(IY+3)
	LD	L,(IY+2)
	LD	(HL),A
	INC	HL
	LD	(HL),'0'
	INC	HL
	LD	(HL),'0'
FINITO	LD	B,4
	INC	IY
	DJNZ	$-2
	POP	BC
	DJNZ	PTSCSC
FINTO1	LD	B,1
	LD	A,(SHPPOS)
	LD	(SHPPS1),A
	SRL	A
	JR	NC,ENTRY2
	INC	B
ENTRY2	LD	HL,SHPTBL
CONT	LD	A,(HL)
	OR	A
	INC	HL
	JR	NZ,CONT
	DJNZ	CONT
	LD	(SHPON),HL
	PUSH	HL
	LD	A,(SHPPOS)
	LD	B,A
	SRL	A
	LD	L,A
	LD	H,0
	LD	DE,3FC0H
	ADD	HL,DE
	LD	(SHPOFF),HL
	EX	DE,HL
	LD	HL,SCREEN-3C00H
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	PUSH	HL
	PUSH	DE
	CALL	PRTSTR
	POP	DE
	PUSH	DE
	LD	HL,SCREEN-3C00H
	EX	DE,HL
	AND	A
	SBC	HL,DE
	EX	DE,HL
	POP	HL
	PUSH	HL
	DEC	HL
	DEC	DE
	LD	BC,8
	LDIR
	POP	DE
	POP	HL
	CALL	PRTSTR
	LD	A,0C3H
	LD	(4000H),A
	LD	A,(3840H)
	LD	B,A
	AND	6
	CP	6
	JR	NZ,CONT12
	JP	ED00H		;Aborted.
CONT12	CALL	SCNKBD
	LD	A,(KBRD)
	AND	12
	JR	Z,NOKEY
	CP	12
	JR	Z,NOKEY
	CP	8
	JR	Z,RIGHT
;*************************
LEFT	LD	A,(SHPPOS)
	DEC	A
	JR	NZ,ENTRY1
	INC	A
	JR	ENTRY1
RIGHT	LD	HL,7FH
	LD	D,0
	LD	E,12
	AND	A
	SBC	HL,DE
	LD	A,(SHPPOS)
	CP	L
	JR	NC,ENTRY1
	INC	A
ENTRY1	LD	(SHPPOS),A
NOKEY	RET
BPOINT	DEFW	5
	DEFW	0
	DEFW	0
BPNTS	DEFW	0
	DEFW	0
	DEFW	0
GYTYP	DEFB	0
BPNTFL	DEFB	0
P1OR2F	DEFB	0
DEMOF	DEFB	0
DEMMOV	DEFB	0
DEMDIR	DEFB	0
SHPPOS	DEFB	0
SHPON	DEFW	0
SHPOFF	DEFW	0
SHPPS1	DEFB	0
CZAPNO	DEFB	0
SHTFIN	DEFB	0
NUMBRS	DEFB	0
SPEED	DEFW	401H
NOPLAY	DEFB	0
TMCRTC	DEFB	0
DJNZL	DEFB	0
XBLSNO	DEFB	0
SCRBF1	DEFB	0
SCRBF2	DEFB	0
NUSHIP	DEFB	0
CONTER	DEFW	0
RNDNO	DEFB	0
SHOT1F	DEFB	0
SHOT2F	DEFB	0
SHOOTF	DEFB	0
SHTPOS	DEFB	0
SHTLOC	DEFW	0
CNTDED	DEFB	0
CNTKIL	DEFB	0
KLTIME	DEFB	1
TIMLIN	DEFB	0
TIMECN	DEFB	0
KBRD	DEFB	0
BULET1	DEFB	0
RSTSCR	DEFM	'000000 : 03'
	DEFB	0
TIMMSG	DEFM	'Time:'
	DEFB	0
MSGBNS	DEFM	'0  Bonus Points'
	DEFB	0
SCPTTB	DEFS	40
SHTTBL	DEFS	48
PRTSTR	LD	A,(HL)
	OR	A
	RET	Z
	EX	DE,HL
	XOR	(HL)
	EX	DE,HL
	LD	(DE),A
	INC	HL
	INC	DE
	JR	PRTSTR
PRNSTR	PUSH	AF
	PUSH	HL
	LD	HL,SCREEN-3C00H
	ADD	HL,DE
	EX	DE,HL
	POP	HL
PRNST1	LD	A,(HL)
	OR	A
	JR	NZ,STAY
	POP	AF
	RET
STAY	CP	20H
	JR	C,CRSRMV
	CP	0C0H
	JR	NZ,ONWARD
	LD	A,80H
	JR	ALPHA
ONWARD	CP	80H
	JR	NC,GRPHIX
	CP	60H
	JR	C,ALPHA
	PUSH	AF
	LD	A,(LOWCAS)
	OR	A
	JR	NZ,ALPHA1
	POP	AF
	SUB	20H
	JR	ALPHA
ALPHA1	POP	AF
ALPHA	LD	(DE),A
	INC	HL
	INC	DE
	JR	PRNST1
CRSRMV	CP	26
	JR	NZ,BACKSP
	PUSH	HL
	LD	HL,64
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	INC	HL
	JR	CHEKCR
BACKSP	CP	24
	JR	NZ,ROMCAL
	DEC	DE
	INC	HL
	JR	CHEKCR
ROMCAL	LD	(4020H),DE
	CALL	33H
	LD	DE,(4020H)
	INC	HL
	JR	CHEKCR
GRPHIX	EX	DE,HL
	OR	(HL)
	EX	DE,HL
	LD	(DE),A
	INC	DE
	INC	HL
	JR	PRNST1
CHEKCR	LD	A,D
	CP	SCRN+5
	JR	NC,SUBTRT
	CP	SCRN
	JR	C,ADDITN
	JR	PRNST1
SUBTRT	PUSH	HL
	LD	HL,64
	EX	DE,HL
	SBC	HL,DE
	EX	DE,HL
	POP	HL
	JR	PRNST1
ADDITN	INC	DE
	JR	PRNST1
LOWCAS	DEFB	1
INCX	PUSH	AF
	LD	A,(IY+2)
	INC	A
	CP	120
	JR	C,GOODX
	LD	A,119
GOODX	LD	(IY+2),A
	POP	AF
	RET
DECX	PUSH	AF
	LD	A,(IY+2)
	DEC	A
	JR	NZ,GOODX
	INC	A
	JR	GOODX
INCY	PUSH	AF
	LD	A,(IY+1)
	INC	A
	CP	43
	JR	NZ,GOODY
	DEC	A
GOODY	LD	(IY+1),A
	POP	AF
	RET
DECY	PUSH	AF
	LD	A,(IY+1)
	DEC	A
	JR	NZ,GOODY
	INC	A
	JR	GOODY
ED00H	LD	HL,VCTRHI
	LD	DE,4000H
	LD	BC,40H
	LDIR
	LD	A,0C9H
	LD	(RET),A
	CALL	SPFX
	LD	A,21H
	LD	(RET),A
	JP	TITLEP
SETARR	LD	A,(P1OR2F)
	LD	C,3CH
	OR	A
	JR	Z,ARRPUT
	LD	C,3EH
ARRPUT	LD	B,3
	LD	HL,SCREEN+0DH
ARRPT1	LD	(HL),C
	INC	HL
	DJNZ	ARRPT1
	LD	B,3
	LD	HL,SCREEN+30H
ARRPT2	LD	(HL),C
	INC	HL
	DJNZ	ARRPT2
	RET
TMLNPT	LD	A,(TIMLIN)
	LD	HL,SCREEN+17H
	SRL	A
	PUSH	AF
	OR	A
	JR	Z,PTDOT
	LD	B,A
PTLIN	LD	(HL),8CH
	INC	HL
	DJNZ	PTLIN
PTDOT	POP	AF
	JR	NC,FIXRST
	LD	(HL),84H
	INC	HL
FIXRST	LD	(HL),80H
	LD	A,0AAH
	LD	(SCREEN+2EH),A
	RET
SCNKBD	LD	A,(DEMOF)
	OR	A
	JR	Z,SCNKB1
	IN	A,(0)
	CPL
	LD	B,A
	LD	A,(38FFH)
	OR	B
	JP	NZ,ED00H	;Abort Demo.
	LD	A,(DEMMOV)
	OR	A
	JR	Z,NUDMOV
	DEC	A
	LD	(DEMMOV),A
SCNKB2	LD	A,(DEMDIR)
	AND	12
	OR	16
	LD	(KBRD),A
	XOR	A
	LD	(SHOOTF),A
	RET
NUDMOV	LD	A,(RNDNO)
	LD	B,4
	BIT	5,A
	JR	NZ,NSLABM
	SLA	B
NSLABM	LD	A,B
	LD	(DEMDIR),A
	LD	A,(RNDNO)
	AND	31
	LD	(DEMMOV),A
	JR	SCNKB2
SCNKB1	IN	A,(0)
	CPL
	AND	31
	LD	B,A
	AND	3
	CP	3
	JR	NZ,NEQU
	LD	A,B
	OR	16
	LD	B,A
NEQU	LD	A,B
	AND	28
	LD	B,A
	LD	A,(3840H)
	AND	224
	SRL	A
	SRL	A
	SRL	A
	OR	B
	LD	B,A
	LD	A,(3801H)
	AND	65
	JR	Z,GTLTSN
	LD	A,B
	OR	16
	LD	B,A
GTLTSN	LD	A,(3820H)
	AND	50H
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	JR	NC,LNTPRS
	INC	A
LNTPRS	SLA	A
	SLA	A
	OR	B
	LD	(KBRD),A
	AND	16
	RET	NZ
	LD	(SHOOTF),A
	RET
AGGRES	CP	11H
	JP	C,EYFITE
	CP	18H
	JP	Z,DRONE
	CP	20H
	JP	Z,ARMOR
	CP	28H
	JP	Z,XCUTNR
	CP	30H
	JP	Z,CYCLOP
FALLER	LD	A,(IY+7)
	OR	A
	JR	NZ,FALR1
	LD	B,1
	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	BIT	2,A
	JR	Z,NSLABF
	SLA	B
NSLABF	LD	A,B
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	LD	(IY+7),A
FALR1	BIT	5,A
	CALL	NZ,INCX
	BIT	4,A
	CALL	NZ,DECX
	LD	A,(IY+2)
	CP	118
	JR	C,FALRX0
FALR2	SET	7,(IY+0)
	RES	6,(IY+0)
	JP	FIGGER
FALRX0	CP	2
	JP	P,FIGGER
	JR	FALR2
CYCLOP	LD	A,(IY+7)
	OR	A
	JR	NZ,CYCNZ
	LD	DE,101H
	LD	A,(RNDNO)
	AND	80H
	JR	NZ,NSLADC
	SLA	D
NSLADC	LD	A,(RNDNO)
	AND	20H
	JR	NZ,NSLAEC
	SLA	E
NSLAEC	SLA	D
	SLA	D
	LD	A,D
	ADD	A,E
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	LD	(IY+7),A
CYCNZ	LD	B,A
	AND	15
	JR	Z,CYCNZ1
	DEC	A
	LD	C,A
	LD	A,B
	AND	0F0H
	OR	C
	LD	(IY+7),A
	JP	FIGGER
CYCNZ1	LD	A,B
	BIT	7,A
	CALL	NZ,INCY
	BIT	6,A
	CALL	NZ,DECY
	BIT	5,A
	CALL	NZ,INCX
	BIT	4,A
	CALL	NZ,DECX
	LD	A,(IY+1)
	CP	42
	JR	NZ,CYCY0
	LD	A,(IY+7)
	XOR	0C0H
	LD	(IY+7),A
	JR	CYCXHI
CYCY0	CP	1
	JR	NZ,CYCXHI
	LD	A,(IY+7)
	XOR	0C0H
	LD	(IY+7),A
CYCXHI	LD	A,(IY+2)
	CP	118
	JR	C,CYCX0
	SET	4,(IY+7)
	RES	5,(IY+7)
	JR	CYCZAP
CYCX0	CP	2
	JR	NC,CYCZAP
	LD	A,(IY+7)
	XOR	30H
	LD	(IY+7),A
CYCZAP	LD	A,(LEVEL)
	OR	A
	NOP
	JP	C,FIGGER
	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	127
CZPFRE	EQU	$-1
	JP	NZ,FIGGER
	LD	A,(IY+2)
	ADD	A,3
	LD	C,A
	LD	A,(IY+1)
	ADD	A,5
	LD	B,A
	PUSH	IY
	LD	IY,SHTTBL
	LD	(IY+0),90H
	LD	(IY+1),B
	LD	(IY+2),C
	POP	IY
	JP	FIGGER
EYFITE	LD	A,(IY+7)
	OR	A
	JR	NZ,EYFIT1
	LD	A,80H
	LD	(IY+7),A
EYFIT1	LD	B,A
	AND	15
	JR	Z,EYFIT2
	DEC	A
	LD	C,A
	LD	A,B
	AND	0F0H
	OR	C
	LD	(IY+7),A
	JP	FIGGER
EYFIT2	CALL	UPDOWN
	CALL	RNDX
PELLET	LD	HL,(CONTER)
	LD	A,(RNDNO)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	15
CHANCE	EQU	$-1
	JP	NZ,FIGGER
	LD	A,(IY+2)
	ADD	A,4
	LD	E,A
	LD	A,(GYTYP)
	CP	1
	JR	NZ,NOINK
	INC	E
NOINK	LD	A,(IY+1)
	ADD	A,6
	LD	D,A
	PUSH	IY
	LD	IY,SHTTBL
	LD	B,8
NOOFSH	EQU	$-1
	LD	A,B
	OR	A
	JR	Z,PELET3
PELET4	PUSH	BC
	BIT	7,(IY+0)
	JR	NZ,PELET2
	LD	(IY+0),0C0H
	LD	A,(RNDNO)
	XOR	(HL)
RNDFX1	AND	0CH
	CP	0CH
	JR	Z,RNDFIX
	OR	(IY+0)
	LD	(IY+0),A
	LD	(IY+1),D
	LD	(IY+2),E
	POP	BC
PELET3	POP	IY
	JP	FIGGER
PELET2	LD	B,5
	INC	IY
	DJNZ	$-2
	POP	BC
	DJNZ	PELET4
	JR	PELET3
RNDFIX	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	JR	RNDFX1
DRONE	LD	A,(IY+7)
	OR	A
	JR	NZ,DRONE1
	LD	A,80H
	LD	(IY+7),A
DRONE1	LD	B,A
	AND	15
	JR	Z,DRONE2
	DEC	A
	LD	C,A
	LD	A,B
	AND	0F0H
	OR	C
	LD	(IY+7),A
	JP	FIGGER
DRONE2	CALL	UPDOWN
	CALL	NRNDX
	JP	PELLET
ARMOR	LD	A,(IY+7)
	OR	A
	JR	NZ,ARMOR1
	LD	A,80H
	LD	(IY+7),A
ARMOR1	AND	15
	JR	NZ,ARMOR3
	CALL	NRNDX
	LD	A,(IY+1)
	CP	41
	JR	NZ,ARMOR3
	LD	A,(IY+7)
	BIT	7,A
	JR	Z,ARMOR3
	OR	15
	LD	(IY+7),A
ARMOR3	CALL	UPDOWN
	JP	FIGGER
XCUTNR	LD	A,(IY+7)
	OR	A
	JR	NZ,XBLAST
	LD	A,80H
	LD	(IY+7),A
	LD	HL,SQNFG8
	LD	(IX+1),H
	LD	(IX+0),L
XBLAST	LD	B,A
	AND	15
	JR	Z,XBLST2
	DEC	A
	LD	C,A
	LD	A,B
	AND	0F0H
	OR	C
	LD	(IY+7),A
	JP	FIGGER
XBLST2	LD	A,(RNDNO)
	LD	HL,(CONTER)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	31
CHNCE1	EQU	$-1
	JP	NZ,RANDM1
	LD	A,(IY+2)
	ADD	A,4
	LD	E,A
	LD	A,(IY+1)
	ADD	A,6
	LD	D,A
	PUSH	IY
	LD	IY,SHTTBL
	LD	B,8
NOFSH1	EQU	$-1
	LD	A,B
	OR	A
	JR	Z,XBLST8
NOFSH2	PUSH	BC
	BIT	7,(IY+0)
	JR	NZ,XBLST9
	LD	(IY+0),0A0H
	LD	(IY+1),D
	LD	(IY+2),E
	LD	(IY+3),D
	LD	(IY+4),E
	POP	BC
	POP	IY
	LD	(IY+7),85H
	JP	FIGGER
XBLST9	LD	B,5
	INC	IY
	DJNZ	$-2
	POP	BC
	DJNZ	NOFSH2
XBLST8	POP	IY
	JP	RANDM1
NRNDX	LD	A,(SHPPOS)
	INC	A
	LD	C,A
	LD	A,(IY+2)
	CP	C
	RET	Z
	JP	C,INCX
	JP	NC,DECX
RNDX	LD	A,(IY+2)
	CP	107
	JR	C,XRITE
	JP	DECX
XRITE	CP	11
	JR	NC,XRND
	JP	INCX
XRND	LD	HL,(CONTER)
	LD	A,(RNDNO)
	XOR	(HL)
	INC	HL
	XOR	(HL)
	INC	HL
	LD	(CONTER),HL
	LD	(RNDNO),A
	AND	3
	JR	Z,XRND
	BIT	0,A
	CALL	NZ,DECX
	BIT	1,A
	CALL	NZ,INCX
	RET
UPDOWN	LD	A,(IY+7)
	BIT	7,A
	CALL	NZ,INCY
	BIT	6,A
	CALL	NZ,DECY
	LD	A,(IY+1)
	CP	42
	JR	NZ,YOYL0
	LD	A,(IY+7)
	LD	C,A
	AND	0F0H
	LD	B,A
	LD	A,C
	AND	0FH
	JR	Z,GOINUP
	DEC	A
	OR	B
	LD	(IY+7),A
	JR	Z,GOINUP
	RET
GOINUP	LD	A,C
	XOR	0C0H
	LD	(IY+7),A
	RET
YOYL0	CP	18
	RET	C
	RET	NZ
	LD	A,(IY+7)
	SET	7,A
	RES	6,A
	LD	(IY+7),A
	RET
LINE	EXX
	LD	A,(ENDX+1)
	LD	B,A
	LD	A,(STARTX+1)
	SUB	B
	JR	NC,NONEG
	NEG
NONEG	LD	C,A
	LD	A,(ENDY+1)
	LD	B,A
	LD	A,(STARTY+1)
	SUB	B
	JR	NC,NONEG1
	NEG
NONEG1	ADD	A,C
	CP	23
	JR	C,NONEG3
	LD	A,(CNTKIL)
	OR	A
	JR	NZ,NONEG3
	EXX
	POP	HL
	LD	HL,ZOT16
	PUSH	HL
	RET
NONEG3	LD	HL,(ENDX)
	LD	DE,(STARTX)
	LD	L,H
	LD	E,D
	LD	H,0
	LD	D,H
	OR	A
	SBC	HL,DE
	LD	(DIRX),HL
	LD	HL,(ENDY)
	LD	DE,(STARTY)
	LD	L,H
	LD	E,D
	LD	H,0
	LD	D,H
	OR	A
	SBC	HL,DE
	LD	(DIRY),HL
	EX	DE,HL
	LD	HL,(DIRX)
	LD	A,L
	AND	80H
	LD	B,A
	LD	A,E
	AND	80H
	LD	C,A
SHIFT	SLA	L
	SLA	E
	LD	A,L
	AND	80H
	CP	B
	JR	NZ,DSHIFT
	LD	A,E
	AND	80H
	CP	C
	JR	Z,SHIFT
DSHIFT	LD	(DIRX),HL
	LD	(DIRY),DE
NXTBLO	CALL	SET
	OR	A
	LD	HL,(ENDX)
	LD	DE,(STARTX)
	SBC	HL,DE
	JR	Z,MAYBE
NOTYET	LD	HL,(STARTX)
	LD	DE,(DIRX)
	ADD	HL,DE
	LD	(STARTX),HL
	LD	HL,(STARTY)
	LD	DE,(DIRY)
	ADD	HL,DE
	LD	(STARTY),HL
	JR	NXTBLO
MAYBE	OR	A
	LD	HL,(ENDY)
	LD	DE,(STARTY)
	SBC	HL,DE
	JR	NZ,NOTYET
	EXX
	RET
SET	LD	DE,(STARTX)
	BIT	7,E
	JR	Z,NXTBIT
	INC	D
NXTBIT	LD	A,D
	LD	BC,(STARTY)
	BIT	7,C
	JR	Z,NXTBT1
	INC	B
NXTBT1	LD	E,0
	SRL	A
	CALL	C,ADDOFF
	LD	L,A
	LD	H,0
	LD	A,B
	PUSH	DE
	LD	DE,64
	OR	A
	SBC	HL,DE
	ADD	A,3
BIT	SUB	3
	ADD	HL,DE
	CP	3
	JR	NC,BIT
	POP	DE
	SLA	A
	ADD	A,E
	LD	DE,3C00H
	ADD	HL,DE
	LD	B,A
	INC	B
	LD	DE,TABLE-1
FINDT	INC	DE
	DJNZ	FINDT
	LD	A,(STORS1)
	OR	A
	JR	NZ,RESET
	LD	A,(DE)
	OR	(HL)
	LD	(HL),A
	BIT	6,(IY+0)
	RET	NZ
	BIT	5,(IY+0)
	JR	Z,XCNTR1
	LD	A,(XCNTR)
	XOR	1
	LD	(XCNTR),A
	RET	Z
XCNTR1	LD	A,(PORT)
	XOR	3
	OUT	(0FFH),A
	LD	(PORT),A
	RET
RESET	LD	A,(DE)
	XOR	0FFH
	AND	(HL)
	OR	80H
	LD	(HL),A
	RET
TABLE	DEFB	1
	DEFB	2
	DEFB	4
	DEFB	8
	DEFB	10H
	DEFB	20H
ADDOFF	INC	E
	RET
PORT	DEFB	1
XCNTR	DEFB	0
STARTX	DEFW	0
STARTY	DEFW	0
ENDX	DEFW	0
ENDY	DEFW	0
DIRX	DEFW	0
DIRY	DEFW	0
STORS1	DEFB	0
	END	ENTRY
